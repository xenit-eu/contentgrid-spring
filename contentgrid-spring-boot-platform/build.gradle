plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

ext {
    springContentVersion = '1.2.7'
}

dependencies {
    api platform('org.springframework.boot:spring-boot-dependencies:3.0.0')
    api platform('com.contentgrid.thunx:thunx-bom:0.5.0')

    constraints {
        api project(':contentgrid-spring-boot-starter')
        api project(':contentgrid-spring-boot-starter-annotations')
        api project(':contentgrid-spring-boot-actuators')

        ['rest', 'fs', 'jpa', 'mongo', 'rest', 's3', 'renditions', 'solr', 'elasticsearch'].each {
            api "com.github.paulcwarren:spring-content-${it}:${springContentVersion}"
            api "com.github.paulcwarren:spring-content-${it}-boot-starter:${springContentVersion}"
        }
        ['autoconfigure', 'azure-storage', 'commons', 'docx4j', 'gcs', 'cmis'].each {
            api "com.github.paulcwarren:spring-content-${it}:${springContentVersion}"
        }
        ['common', 'jpa', 'jpa-boot-starter'].each {
            api "com.github.paulcwarren:spring-versions-${it}:${springContentVersion}"
        }
    }
}