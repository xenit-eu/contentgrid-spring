plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    constraints {
        api project(':contentcloud-spring-boot-starter')
        api project(':contentcloud-spring-boot-starter-annotations')

        api "eu.xenit.contentcloud.thunx:thunx-api-spring-boot-starter:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-visitor-reducer:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-spring:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-predicates-querydsl:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-pdp-opa:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-pdp:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-model:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-gateway-spring-boot-starter:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-encoding-json:${thunxVersion}"
        api "eu.xenit.contentcloud.thunx:thunx-autoconfigure:${thunxVersion}"

        ['rest', 'fs', 'jpa', 'mongo', 'rest', 's3', 'renditions', 'solr', 'elasticsearch'].each {
            api "com.github.paulcwarren:spring-content-${it}:${springContentVersion}"
            api "com.github.paulcwarren:spring-content-${it}-boot-starter:${springContentVersion}"
        }
        ['autoconfigure', 'azure-storage', 'commons', 'docx4j', 'gcs', 'cmis'].each {
            api "com.github.paulcwarren:spring-content-${it}:${springContentVersion}"
        }
        ['common', 'jpa', 'jpa-boot-starter'].each {
            api "com.github.paulcwarren:spring-versions-${it}:${springContentVersion}"
        }

    }

}