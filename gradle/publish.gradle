apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {

        all {
            pom {
                url = 'https://github.com/xenit-eu/contentgrid-spring'
                name = project.name
                description = project.description

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/contentgrid-spring.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/contentgrid-spring.git'
                    url = 'https://github.com/xenit-eu/contentgrid-spring.git'
                }

                developers {
                    developer {
                        name = "XeniT"
                        organization = "XeniT Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }

    repositories {
        if ("${project.version}".endsWith('-SNAPSHOT')) {
            sonatypeSnapshots {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = project.findProperty('sonatype_username')
                    password = project.findProperty('sonatype_password')
                }
            }
        } else {
            sonatypeMavenCentral {
                url = "https://s01.oss.sonatype.org/service/local/"
                credentials {
                    username = project.findProperty('sonatype_username')
                    password = project.findProperty('sonatype_password')
                }
            }
        }
    }
}

pluginManager.withPlugin('java-library') {
    publishing {
        publications {
            library(MavenPublication) {
                from components.java

                versionMapping {
                    usage("java-api") {
                        fromResolutionResult()
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}

pluginManager.withPlugin('java-platform') {
    publishing {
        publications {
            platform(MavenPublication) {
                from components.javaPlatform
            }
        }
    }
}

pluginManager.withPlugin('base') {
    tasks.named('check').configure {
        dependsOn('checkMavenCentralRequirements')
    }
}