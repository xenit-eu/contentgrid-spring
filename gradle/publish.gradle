apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jreleaser'

publishing {
    publications {

        all {
            pom {
                url = 'https://github.com/xenit-eu/contentgrid-spring'
                name = project.name
                description = project.description

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/contentgrid-spring.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/contentgrid-spring.git'
                    url = 'https://github.com/xenit-eu/contentgrid-spring.git'
                }

                developers {
                    developer {
                        name = "XeniT"
                        organization = "XeniT Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    applyMavenCentralRules = true
                    stagingRepository('build/staging-deploy')
                    namespace = 'com.contentgrid'
                }
            }
            nexus2 {
                'sonatype-snapshot' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    sign = false
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    // It is required to configure at least one release provider.
    // But you can provide any non-existing non-empty GitHub token and skip creating tags and releases.
    // see https://github.com/jreleaser/jreleaser/discussions/1725
    gitRootSearch = true
    release {
        github {
            enabled = true
            skipTag = true
            skipRelease = true
            token = '__UNUSED__'
        }
    }
}

pluginManager.withPlugin('java-library') {
    publishing {
        publications {
            library(MavenPublication) {
                from components.java

                versionMapping {
                    usage("java-api") {
                        fromResolutionResult()
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}

pluginManager.withPlugin('java-platform') {
    publishing {
        publications {
            platform(MavenPublication) {
                from components.javaPlatform
            }
        }
    }
}

pluginManager.withPlugin('base') {
    tasks.named('check').configure {
        dependsOn('checkMavenCentralRequirements')
    }
}